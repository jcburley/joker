;;;; See DEVELOPER.md for information on this script.

;;; Load all core namespaces. This pulls in any std namespaces upon which
;;; they depend. Then, set 'preloaded' to those std namespaces, forcing
;;; complete evaluation so lazy evaluation wouldn't find joker.os due to
;;; the subsequent require.
(doseq [ns (remove #(= % 'user) joker.core/*core-namespaces*)] (require ns))
(def preloaded (set (remove joker.core/*core-namespaces* (filter #(joker.core/ns-initialized? %) (map #(symbol (str %)) (all-ns))))))

(require '[joker.string :as s]
         '[joker.os :as os])

(def JOKER_STD_OS (or (keyword (joker.os/get-env "JOKER_STD_OS"))
                      :darwin))

(def rpl s/replace)

(defn debug
  [& args]
  (when ((os/env) "JOKER_DEBUG_STD")
    (apply println args))
  (get args 0))

(debug "Preloaded:" preloaded)

;;; Discover namespaces dynamically by finding *.joke files.
(def namespaces
  (vec (->> (os/ls ".")
            (remove :dir?)
            (map :name)
            (remove #(= "generate-std.joke" %))
            (filter #(s/ends-with? % ".joke"))
            (map #(rpl % #"[.]joke$" ""))
            (map symbol))))

(debug "Namespaces:" namespaces)
(apply require :reload namespaces) ; :reload in case namespaces become 'joker.base64 etc.

(def fn-template
  (slurp "fn.tmpl"))

(def arity-template
  (slurp "arity.tmpl"))

(def package-template
  (slurp "package.tmpl"))

(def package-slow-template
  (slurp "package-slow.tmpl"))

(def package-fast-template
  (slurp "package-fast.tmpl"))

(def intern-template
  (slurp "intern.tmpl"))

(def addmeta-template
  (s/trim-right (slurp "addmeta.tmpl")))

(defn q
  [s]
  (str "\"" s "\""))

(defn raw-quoted-string
  "Returns a Go-style backtick-quoted string with backticks handled by appending double-quoted backticks"
  [s]
  (str "`" (rpl s "`" "` + \"`\" + `") "`"))

(defn go-name
  "Convert Clojure-style function name to unique Go-style name suitable as its internal implementation."
  [fn-name]
  (let [n (-> fn-name
              (rpl "-" "_")
              (rpl "?" "")
              (str "_"))]
    (if (s/ends-with? fn-name "?")
      (str "is" n)
      n)))

(defn resolve-package
  [pkg ns-name imports]
  (if (= pkg ns-name)
    ""  ; it me
    (do ;;(println pkg ns-name imports)
        (str (get (get imports pkg [(str pkg) "FAIL"]) 0) "."))))

(defn extract-arg
  "Generate code to extract a single argument from an argument list, using either the native Extract<Type>() or the autogenerated Extract_ns_<Type>() routine."
  [m ns-name imports]
  (let [fulltype (rpl (str (:tag m)) "*" "refTo")]
    (if (s/includes? fulltype "/")
      (let [spl (s/split fulltype "/")
            pkg (spl 0)
            t (spl 1)
            t (cond-> t
                (:varargs m) (str "_s"))]
        (str (resolve-package pkg ns-name imports) "Extract_ns_" t))
      (let [t (cond-> fulltype
                (:varargs m) (str "s"))]
        (str "Extract" t)))))

(defn extract-args
  "Generate code to extract the arguments in the list, assigning the extracted values to variable names corresponding to the argument names."
  [args ns-name imports]
  (s/join
   "\n\t\t"
   (map-indexed
    (fn [i arg]
      (let [m (meta arg)
            extract (extract-arg m ns-name imports)]
        (str arg " := " extract "(_args, " (str i) ")")))
    args)))

(defn handle-varargs
  [args]
  (let [c (count args)]
    (if (and (> c 1)
             (= '& (nth args (- c 2))))
      (let [vargs (vary-meta (last args) assoc :varargs true)]
        (conj (subvec args 0 (- c 2)) vargs))
      args)))

(defn ^:private type-name
  [tag]
  (if (vector? tag)
    (str (first tag) "Vector")
    (str tag)))

(defn generate-arity
  [args imports go tag ns-name]
  (let [handle-args (handle-varargs args)
        cnt (count handle-args)
        varargs? (< cnt (count args))
        go-expr (cond
                  (string? go) go
                  varargs? (:varargs go)
                  :else (get go cnt))
        go-res (if (s/starts-with? go-expr "!")
                 (subs go-expr 1)
                 (str "_res := " go-expr))]
    (-> arity-template
        (rpl "{arity}" (if varargs? "true" (str "_c == " (count args))))
        (rpl "{arityCheck}" (if varargs?
                              (str "CheckArity(_args, " (dec cnt) ", " 999 ")")
                              "{blank}"))
        (rpl "{args}" (if (empty? args)
                        "{blank}"
                        (extract-args handle-args ns-name imports)))
        (rpl "{goExpr}" (rpl go-res "; " "\n\t\t"))
        (rpl "{return}"
             (if tag
               (str "return Make" (type-name tag) "(_res)")
               "return _res")))))

(defn generate-arglist
  [args]
  (str "NewVectorFrom("
       (s/join ", " (for [arg args]
                      (str "MakeSymbol(" (q (str arg)) ")")))
       ")"))

(defn make-value
  "Returns code to make the Joker object representing the given value.

  E.g. 'String{S: \"value\"}'. Except for integers, the values are treated as strings (for now)."
  [v]
  (condp = (str (type v))
    "Int" (str "Int{I: " v "}")
    (str "String{S: " (q v) "}")))

(defn add-other-meta
  "Append meta tags other than what are normally present or irrelevant (:go)."
  [m]
  (let [m (dissoc m :doc :added :arglists :ns :name :file :line :column :go)]
    (s/join "" (map #(-> addmeta-template
                         (rpl "{key}" (s/replace-first (str (key %)) ":" ""))
                         (rpl "{value}" (make-value (val %)))) m))))

(defn generate-fn-decl
  [ns-name ns-name-final imports k v]
  (debug "Processing function" v)
  (let [m (meta v)
        _ (when-not (:doc m)
            (throw (ex-info (str "Missing docstring for function declaration: " k) {})))
        arglists (:arglists m)
        go-fn-name (go-name (str k))
        arities (s/join "\n\t" (map #(generate-arity % imports (:go m) (:tag m) ns-name) arglists))
        fn-str (-> fn-template
                   (rpl "{goName}" go-fn-name)
                   (rpl "{pkg}" ns-name-final)
                   (rpl "{fnName}" (str k))
                   (rpl "{arities}" arities))
        intern-str (-> intern-template
                       (rpl "{nsFullName}" ns-name)
                       (rpl "{nsName}" ns-name-final)
                       (rpl "{autoDeref}" "")
                       (rpl "{fnName}" (str k))
                       (rpl "{goName}" go-fn-name)
                       (rpl "{fnDocstring}" (raw-quoted-string (:doc m)))
                       (rpl "{added}" (:added m))
                       (rpl "{moreMeta}" (add-other-meta m))
                       (rpl "{args}"
                            (str "NewListFrom("
                                 (s/join ", " (for [args arglists]
                                                (generate-arglist args)))
                                 ")")))]
    [fn-str intern-str]))

(defn go-return-type
  "Returns the return type of the Make<t>() function. Would be unnecessary if Go code could declare a var as having 'the type returned by <func>'."
  [t]
  (condp = t
    "BigInt" "*BigInt"
    "BigIntU" "*BigInt"
    "BigFloat" "*BigFloat"
    "Ratio" "*Ratio"
    "Number" "*BigInt"
    "StringVector" "*Vector"
    "Error" "String"
    t))

(defn generate-const-or-var-decl
  [name m]
  (let [type (type-name (:tag m))]
    (cond (= type "Var")
          (format "var %s GoObject" name)
          (= type "Type")
          ""  ; Intern directly from call to avoid init-order issues
          :else
          (format "var %s %s" name (go-return-type type)))))

(defn generate-const-or-var-init
  [ns-name-final full-name go-name m]
  (let [type (type-name (:tag m))
        go (:go m)
        v (if (map? go)
            (or (get go JOKER_STD_OS)
                (:default go))
            go)]
    (when v
    (cond (= type "Var")
          (format "\t%s = MakeGoObject(&%s)"  ; Get pointer to the actual var
                  go-name
                  (:go m))
          (= type "Type")
          ""  ; Intern directly from call to avoid init-order issues
          :else
          (format "\t%s = Make%s(%s)"
                  go-name
                  type
                  v)))))

(defn direct-or-indirect-init
  [m name]
  (let [type (type-name (:tag m))]
    (cond (not= type "Type")
          name
          :else  ; Intern directly from call to avoid init-order issues
          (:go m))))

(defn generate-non-fn-decl
  [ns-name ns-name-final k v]
  (let [m (meta v)
        go (:go m)]
    (if (and (map? go)
             (not (contains? go :default))
             (not (contains? go JOKER_STD_OS)))
      nil
      (let [go-non-fn-name (direct-or-indirect-init m (go-name (str k)))
        non-fn-str (generate-const-or-var-decl go-non-fn-name m)
        auto-deref (if (= (type-name (:tag m)) "Var") "AutoDeref" "")
        intern-str (-> intern-template
                       (rpl "{nsFullName}" ns-name)
                       (rpl "{nsName}" ns-name-final)
                       (rpl "{autoDeref}" auto-deref)
                       (rpl "{fnName}" (str k))
                       (rpl "{goName}" go-non-fn-name)
                       (rpl "{fnDocstring}" (raw-quoted-string (:doc m)))
                       (rpl "{added}" (:added m))
                       (rpl "{moreMeta}" (add-other-meta m))
                       (rpl "{args}" "nil"))]
    [non-fn-str intern-str]))))

(defn generate-non-fn-init
  [ns-name ns-name-final k v]
  (debug "Processing non-fn-init" v)
  (let [m (meta v)
        orig-name (str k)
        go-non-fn-name (go-name orig-name)]
    (generate-const-or-var-init ns-name-final (str ns-name "/" orig-name) go-non-fn-name m)))

(defn comment-out
  [s]
  (-> s
      (rpl "\n// " "\n")
      (rpl "\n" "\n//")
      (rpl "\n// package" "\npackage")))

(defn compare-imports
  [^String l ^String r]
  (cond
    (s/starts-with? l ". ") (if (s/starts-with? r ". ")
                              (compare l r)
                              -1)
    (s/starts-with? r ". ") 1
    :else (compare l r)))

(defn- ns-public-fns
  "Return only publics that are functions."
  [ns]
  (remove #(:macro (meta (val %)))
          (filter #(:arglists (meta (val %))) (ns-publics ns))))

(defn- ns-public-non-fns
  "Return only publics that are not functions."
  [ns]
  (remove #(:macro (meta (val %)))
          (remove #(:arglists (meta (val %))) (ns-publics ns))))

(defn- ns-public-go-fns
  "Return only publics that are functions and have additional Go-specific metadata."
  [ns]
  (filter #(:go (meta (val %))) (ns-public-fns ns)))

(defn- ns-public-go-non-fns
  "Return only publics that are not functions and have additional Go-specific metadata."
  [ns]
  (filter #(:go (meta (val %))) (ns-public-non-fns ns)))

(defn- warn-about-skipped-publics
  [skipped]
  (when (> (count skipped) 0)
    (println-err "WARNING: skipping publics that are not Go-calling functions or vars:" skipped)))

(defn- generate-imports
  "The joker.* namespaces supply simple vectors. The gostd tool provides maps: the key of each map is the full name of the namespace; the value is a two-element vector, the first being the local name of the namespace, and the second the full import specification."
  [m]
  (if (vector? m)
    (mapv q m)
    (mapv #(str ((val %) 0) " " (q ((val %) 1))) m)))

(defn generate-ns
  [ns-sym ns-name ns-name-final]
  (debug ns-sym)
  (let [ns (find-ns ns-sym)
        m (meta ns)
        go-non-fns (sort-by first (ns-public-go-non-fns ns))
        go-fns (sort-by first (ns-public-go-fns ns))
        fn-decls (for [[k v] go-fns]
                   (generate-fn-decl ns-name ns-name-final (:go-imports m) k v))
        non-fn-decls (remove nil?
                             (for [[k v] go-non-fns]
                               (generate-non-fn-decl ns-name ns-name-final k v)))
        non-fn-inits (remove nil?
                             (for [[k v] go-non-fns]
                               (generate-non-fn-init ns-name ns-name-final k v)))
        res (-> package-template
                (rpl "{nsFullName}" ns-name)
                (rpl "{nsName}" ns-name-final)
                (rpl "{imports}"
                     (s/join "\n\t" (sort compare-imports (conj
                                                           (generate-imports (:go-imports m))
                                                           ". \"github.com/candid82/joker/core\""))))
                (rpl "{non-fn-decls}" (s/join "\n" (filter #(> (count %) 0) (map first non-fn-decls))))
                (rpl "{non-fn-inits}" (s/join "\n" (filter #(> (count %) 0) non-fn-inits)))
                (rpl "{fn-decls}" (s/join "\n" (map first fn-decls))))
        res (if (:empty m)
              (comment-out res)
              res)]
    (warn-about-skipped-publics (remove (set (concat (map key go-fns)
                                                     (map key go-non-fns)))
                                        (map #(key %) (ns-publics ns))))
    res))

(defn generate-ns-slow-init
  [ns-sym ns-name ns-name-final pre]
  (let [ns (find-ns ns-sym)
        m (meta ns)
        go-non-fns (sort-by first (ns-public-go-non-fns ns))
        go-fns (sort-by first (ns-public-go-fns ns))
        fn-decls (for [[k v] go-fns]
                   (generate-fn-decl ns-name ns-name-final (:go-imports m) k v))
        non-fn-decls (remove nil?
                             (for [[k v] go-non-fns]
                               (generate-non-fn-decl ns-name ns-name-final k v)))
        res (-> package-slow-template
                (rpl "{maybeSlowOnly}" (if pre "//go:build gen_code\n// +build gen_code\n" ""))
                (rpl "{nsFullName}" ns-name)
                (rpl "{nsName}" ns-name-final)
                (rpl "{imports}" ". \"github.com/candid82/joker/core\"")
                (rpl "{nsDocstring}" (raw-quoted-string (:doc m)))
                (rpl "{non-fn-interns}" (s/join "\n\t" (map second non-fn-decls)))
                (rpl "{fn-interns}" (s/join "\n\t" (map second fn-decls))))
        res (if (:empty m)
              (comment-out res)
              res)]
    res))

(defn generate-thunk
  [ns-name ns-name-final k v]
  (let [fn (go-name (str k))]
    (format "STD_thunk_%s_%s_var = __%s" ns-name-final fn fn)))

(defn generate-ns-fast-init
  [ns-sym ns-name ns-name-final]
  (let [ns (find-ns ns-sym)
        m (meta ns)
        thunks (for [[k v] (sort-by first (ns-public-go-fns ns))]
                 (generate-thunk ns-name ns-name-final k v))
        res (-> package-fast-template
                (rpl "{nsName}" ns-name-final)
                (rpl "{imports}" ". \"github.com/candid82/joker/core\"")
                (rpl "{thunks}" (s/join "\n\t" thunks)))
        res (if (:empty m)
              (comment-out res)
              res)]
    res))

(defn ns-file-name
  [dir ns-name-final]
  (str dir "/a_" ns-name-final ".go"))

(defn ns-file-name-slow-init
  [dir ns-name-final]
  (str dir "/a_" ns-name-final "_slow_init.go"))

(defn ns-file-name-fast-init
  [dir ns-name-final]
  (str dir "/a_" ns-name-final "_fast_init.go"))

(defn remove-blanky-lines
  [s]
  (-> s
      (rpl #"[[:space:]]*{blank}" "")))

(defn joker-ize
  "Convert a single-element namespace symbol to joker.namespace"
  ^Symbol [^Symbol s]
  (symbol (str "joker." s)))

(doseq [ns-sym namespaces]
  (let [ns-name (str "joker." ns-sym)
        dir (rpl (str ns-sym) "." "/")
        ns-name-final (rpl ns-name #".*[.]" "")
        pre (preloaded (joker-ize ns-sym))]
    (debug "Processing namespace" ns-name "in" dir "final name" ns-name-final)
    (spit (ns-file-name dir ns-name-final)
          (remove-blanky-lines (generate-ns ns-sym ns-name ns-name-final)))
    (spit (ns-file-name-slow-init dir ns-name-final)
          (remove-blanky-lines (generate-ns-slow-init ns-sym ns-name ns-name-final pre)))
    (when pre
      (spit (ns-file-name-fast-init dir ns-name-final)
           (remove-blanky-lines (generate-ns-fast-init ns-sym ns-name ns-name-final))))))

;;; Handle "custom" (gostd-generated) namespaces.  These are placed in
;;; std/gostd/ ("gostd" is specified once, as the value for the
;;; `custom-dir` variable), which nicely segregates them, but which
;;; also complicates this script somewhat. In particular, the "gostd"
;;; pathname prefix must be removed when inferring namespaces, and it
;;; must be restored when loading .joke files and when writing .go
;;; files.

(def custom-dir "gostd")

(defn drop-first-component
  [path]
  (get (s/split path "/" 2) 1))

(defn new-custom-namespaces-here
  [dir]
  (->> (joker.os/ls dir)
       (remove :dir?)
       (map :name)
       (filter #(s/ends-with? % ".joke"))
       (map #(rpl % #"[.]joke$" ""))
       (map symbol)
       (map #(str (drop-first-component dir) "/" %))))

(defn find-dirs
  [dir]
  (->> (joker.os/ls dir)
       (filter :dir?)
       (map :name)))

(defn walk-dirs
  [dir]
  (let [d (find-dirs dir)
        dirs (map #(str dir "/" %) d)
        subdirs (mapcat #(walk-dirs %) dirs)]
    (if (empty? subdirs)
      dirs
      (apply concat (list dirs subdirs)))))

(def custom-namespaces (mapv (comp symbol #(rpl % "/" "."))
                             (sort (mapcat new-custom-namespaces-here
                                           (try (walk-dirs custom-dir)
                                                (catch Error e nil))))))

(debug custom-namespaces)

(when (seq custom-namespaces)
  (binding [joker.core/*classpath* [(str (os/cwd) "/" custom-dir "/")]]
    (apply require :reload custom-namespaces)))

(doseq [ns-sym custom-namespaces]
  (let [ns-name (str ns-sym)
        dir (str custom-dir "/" (rpl ns-name "." "/"))
        ns-name-final (rpl ns-name #".*[.]" "")
        pre (preloaded (joker-ize ns-sym))]
    (debug "Processing custom namespace" ns-name "in" dir "final name" ns-name-final)
    (spit (ns-file-name dir ns-name-final)
          (remove-blanky-lines (generate-ns ns-sym ns-name ns-name-final)))
    (spit (ns-file-name-slow-init dir ns-name-final)
          (remove-blanky-lines (generate-ns-slow-init ns-sym ns-name ns-name-final pre)))
    (when pre
      (spit (ns-file-name-fast-init dir ns-name-final)
            (remove-blanky-lines (generate-ns-fast-init ns-sym ns-name ns-name-final))))))
